apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

targetCompatibility = 1.8
sourceCompatibility = 1.8

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath deps.protobuf.gradle_plugin
    }
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        debugKey {
            keyAlias 'aosp'
            keyPassword 'nexuslab'
            storeFile file('../keys/platform.jks')
            storePassword 'nexuslab'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debugKey
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.all {
            maxHeapSize "2g"
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
        unitTests.includeAndroidResources = true
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

protobuf {
    protoc {
        artifact = deps.protobuf.compiler
    }
    plugins {
        javalite {
            artifact = deps.protobuf.javalite
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    // Annotation Processing
    implementation deps.classindex
    annotationProcessor deps.classindex
    compileOnly deps.lombok.annotation
    annotationProcessor deps.lombok.annotation

    // Protobuf
    implementation deps.protobuf.lite
    implementation deps.protobuf.lite_converter

    // Android
    implementation deps.androidx.appcompat

    // Crypto
    implementation deps.bouncycastle.bcpkix
    implementation deps.bouncycastle.bcprov

    // Utilities
    implementation deps.commons.io
    implementation deps.commons.lang3
    implementation deps.commons.collections4
    implementation deps.commons.text
    implementation deps.okhttp
    implementation deps.snakeyaml
    implementation deps.android_device_names

    // Test
    testCompileOnly deps.lombok.annotation
    testAnnotationProcessor deps.lombok.annotation
    testCompileOnly deps.classindex
    testAnnotationProcessor deps.classindex
    testImplementation deps.junit
    testImplementation deps.mock_web_server
    testImplementation(deps.robolectric) {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    testImplementation deps.androidx.test_core
    testImplementation(deps.faker) {
        exclude group: 'org.yaml:snakeyaml', module: 'snake-yaml'
    }
    androidTestImplementation deps.androidx.test_core
    androidTestImplementation deps.androidx.test_runner
    androidTestImplementation deps.androidx.test_ext
    androidTestImplementation deps.androidx.test_rules
    androidTestImplementation(deps.faker) {
        exclude group: 'org.yaml:snakeyaml', module: 'snake-yaml'
    }
}
